package TicketMachine
public
	with Base_Types;
	with Data_Model;
	with sei;

	data CurrencyType
		properties
			Data_Model::Data_Representation => Fixed;
	end CurrencyType;

	data implementation CurrencyType.impl
	end CurrencyType.impl;

	data PrinterData
		properties
			Data_Model::Data_Representation => String;
	end PrinterData;

	data implementation PrinterData.impl
	end PrinterData.impl;

	data GsmData
	end GsmData;

	data implementation GsmData.impl
	end GsmData.impl;

	data TouchData
	end TouchData;

	data implementation TouchData.impl
	end TouchData.impl;

	bus ethernet
		properties
			SEI::GrossWeight => 0.6kg;
			Transmission_Time => [Fixed => 1ns..2ns; PerByte => 5ns ..6ns;];
    		Period => 1 ms;
    		SEI::BandWidthCapacity => 100.0 MBytesps;
    		SEI::BandWidthBudget => 97.0 MBytesps;
	end ethernet;

	bus implementation ethernet.impl
	end ethernet.impl;

	bus Marine
		properties
			SEI::GrossWeight => 0.1kg;
			Transmission_Time => [Fixed => 1ns..2ns; PerByte => 1ns ..3ns;];
    		Period => 1 us;
    		SEI::BandWidthCapacity => 1024.0 MBytesps;
    		SEI::BandWidthBudget => 500.0 MBytesps;
	end Marine;

	bus implementation Marine.Standard
	end Marine.Standard;

	device Printer
		features
			PrintData: in data port PrinterData;
			PrintComplete: out data port Base_Types::Boolean;
			eth: requires bus access ethernet;
		properties
			SEI::GrossWeight => 0.700kg;
	end Printer;

	device implementation Printer.impl
	end Printer.impl;

	device TouchPanel
		features
			UIEvents: out data port TouchData;
			eth: requires bus access ethernet;
		properties
			SEI::GrossWeight => 0.500kg;
	end TouchPanel;

	device implementation TouchPanel.impl
	end TouchPanel.impl;

	device Lcd
		features
			DispImg: in data port;
			eth: requires bus access ethernet;
		properties
			SEI::GrossWeight => 0.770kg;
	end Lcd;

	device implementation Lcd.impl
	end Lcd.impl;

	device CoinSlot
		features
			Enable: in data port Base_Types::Boolean;
			CoinAmount: out data port CurrencyType;
			ReturnChange: in data port CurrencyType;
			eth: requires bus access ethernet;
		properties
			SEI::GrossWeight => 1.10kg;
	end CoinSlot;

	device implementation CoinSlot.impl
	end CoinSlot.impl;

	device CardTerminal
		features
			Enable: in data port Base_Types::Boolean;
			PaymentInfo: in data port Base_Types::string;
			PaymentSuccess: out data port Base_Types::Boolean;
			eth: requires bus access ethernet;
		properties
			SEI::GrossWeight => 0.35kg;
	end CardTerminal;

	device implementation CardTerminal.impl
	end CardTerminal.impl;

	device GsmAntenna
		features
			DataInOut: in out data port GsmData;
			eth: requires bus access ethernet;
		properties
			SEI::GrossWeight => 0.100kg;
	end GsmAntenna;

	device implementation GsmAntenna.impl
	end GsmAntenna.impl;

	thread TouchController
		features
			UIEvents: in data port TouchData;
	end TouchController;

	thread implementation TouchController.impl
	end TouchController.impl;

	thread UserInterface
		features
			Display: out data port;
	end UserInterface;

	thread implementation UserInterface.impl
	end UserInterface.impl;

	thread PrinterController
		features
			Printer: out data port PrinterData;
			PrinterStatus: in data port Base_Types::Boolean;
	end PrinterController;

	thread implementation PrinterController.impl
	end PrinterController.impl;

	thread PaymentController
		features
			CoinSlotActivate: out data port Base_Types::Boolean;
			CoinSlot: in data port CurrencyType;
			ReturnCoinChange: out data port CurrencyType;
			CardTerminalActivate: out data port Base_Types::Boolean;
			CardPaymentInfo: out data port Base_Types::String;
			CardPaymentSuccess: in data port Base_Types::Boolean;
	end PaymentController;

	thread implementation PaymentController.impl
	end PaymentController.impl;

	thread TicketSeller
		features
			Gsm: in out data port GsmData;
	end TicketSeller;

	thread implementation TicketSeller.impl
	end TicketSeller.impl;

	memory RAM
		features
			BA1: requires bus access Marine.Standard;
		properties
			SEI::GrossWeight => 0.1kg;
	end RAM;

	memory implementation RAM.impl
	end RAM.impl;

	processor corei5
		features
			eth: requires bus access ethernet;
			BA1: requires bus access Marine.Standard;
		properties
			SEI::GrossWeight => 0.05kg;
			Scheduling_Protocol => (Round_Robin_Protocol);
			Clock_Period => 1ms;
			Timing_Properties::Processor_Capacity => 1.0 MIPS;
			SEI::MIPSCapacity => 1.2mips;
	end corei5;

	processor implementation corei5.impl
	end corei5.impl;

	process MainProc
		features
			touch: in data port TouchData;
			display: out data port;
			printer: out data port PrinterData;
			printerStatus: in data port Base_Types::Boolean;
			gsm: in out data port GsmData;
			coinActivate: out data port Base_Types::Boolean;
			coin: in data port CurrencyType;
			changeCoin: out data port CurrencyType;
			cardActivate: out data port Base_Types::Boolean;
			card: out data port Base_Types::String;
			cardSuccess: in data port Base_Types::Boolean;
		properties
			SEI::MIPSBudget => 1.0mips;
	end MainProc;

	process implementation MainProc.impl
		subcomponents
			this_MainThr: thread TicketSeller.impl;
			this_TouchThr: thread TouchController.impl;
			this_UIThr: thread UserInterface.impl;
			this_PrintThr: thread PrinterController.impl;
			this_PayThr: thread PaymentController.impl;
		connections
			touchConn: port touch -> this_TouchThr.UIEvents;
			displayConn: port this_UIThr.Display -> display;
			printerConn: port this_PrintThr.Printer -> printer;
			printerStatusConn: port printerStatus -> this_PrintThr.PrinterStatus;
			gsmConn: port this_MainThr.Gsm <-> gsm;
			coinActivateConn: port this_PayThr.CoinSlotActivate -> coinActivate;
			coinConn: port coin -> this_PayThr.CoinSlot;
			changeCoinConn: port this_PayThr.ReturnCoinChange -> changeCoin;
			cardActivateConn: port this_PayThr.CardTerminalActivate -> cardActivate;
			cardConn: port this_PayThr.CardPaymentInfo -> card;
			cardSuccessConn: port cardSuccess -> this_PayThr.CardPaymentSuccess;
		
		properties
			SEI::BandWidthBudget => 3.0 MBytesps applies to touchConn;
			SEI::BandWidthBudget => 10.0 MBytesps applies to displayConn;
			SEI::BandWidthBudget => 3.0 MBytesps applies to printerConn;
			SEI::BandWidthBudget => 0.1 MBytesps applies to printerStatusConn;
			SEI::BandWidthBudget => 5.0 MBytesps applies to gsmConn;
			SEI::BandWidthBudget => 0.1 MBytesps applies to coinActivateConn;
			SEI::BandWidthBudget => 0.5 MBytesps applies to coinConn;
			SEI::BandWidthBudget => 0.5 MBytesps applies to changeCoinConn;
			SEI::BandWidthBudget => 1.0 MBytesps applies to cardActivateConn;
			SEI::BandWidthBudget => 5.0 MBytesps applies to cardConn;
			SEI::BandWidthBudget => 0.2 MBytesps applies to cardSuccessConn;
	end MainProc.impl;

	system TicketMachine
		properties
			SEI::GrossWeight => 4.370kg;
	end TicketMachine;

	system implementation TicketMachine.impl
		subcomponents
			Main_cpu: processor corei5;
			thisRAM: memory RAM.impl;
			thisMainProc: process MainProc.impl;
			thisPrinter: device Printer.impl;
			thisTouchPanel: device TouchPanel.impl;
			thisLcd: device Lcd.impl;
			thisCoinSlot: device CoinSlot.impl;
			thisCardTerminal: device CardTerminal.impl;
			thisGsmAntenna: device GsmAntenna.impl;
			thisEthernet: bus ethernet.impl;
			thisMarine: bus Marine.Standard;
		connections
			BAC1: bus access thisMarine <-> thisRAM.BA1;
			BAC2: bus access thisMarine <-> Main_cpu.BA1;
			eth0: bus access thisEthernet <-> Main_cpu.eth;
			eth1: bus access thisEthernet <-> thisPrinter.eth;
			eth2: bus access thisEthernet <-> thisTouchPanel.eth;
			eth3: bus access thisEthernet <-> thisLcd.eth;
			eth4: bus access thisEthernet <-> thisCoinSlot.eth;
			eth5: bus access thisEthernet <-> thisCardTerminal.eth;
			eth6: bus access thisEthernet <-> thisGsmAntenna.eth;
			printerConn: port thisMainProc.printer -> thisPrinter.PrintData;
			printerStatusConn: port thisPrinter.PrintComplete -> thisMainProc.printerStatus;
			touchPanelConn: port thisTouchPanel.UIEvents -> thisMainProc.touch;
			lcdConn: port thisMainProc.display -> thisLcd.DispImg;
			coinActivateConn: port thisMainProc.coinActivate -> thisCoinSlot.Enable;
			coinConn: port thisCoinSlot.CoinAmount -> thisMainProc.coin;
			retChangeConn: port thisMainProc.changeCoin -> thisCoinSlot.ReturnChange;
			cardActivateConn: port thisMainProc.cardActivate -> thisCardTerminal.Enable;
			cardConn: port thisMainProc.card -> thisCardterminal.PaymentInfo;
			cardSucessConn: port thisCardTerminal.PaymentSuccess -> thisMainProc.cardSuccess;
			gsmConn: port thisMainProc.gsm <-> thisGsmAntenna.DataInOut;
		properties
			SEI::WeightLimit => 5.0kg;
			SEI::MIPSCapacity => 1.2mips;
			Actual_Processor_Binding => (reference (Main_cpu)) applies to thisMainProc;
			Actual_Memory_Binding => (reference (thisRAM)) applies to thisMainProc;
			Actual_Connection_Binding => (reference (thisMarine)) applies to BAC1;
			Actual_Connection_Binding => (reference (thisMarine)) applies to BAC2;
			Actual_Connection_Binding => (reference (thisEthernet)) applies to eth0;
			Actual_Connection_Binding => (reference (thisEthernet)) applies to eth1;
			Actual_Connection_Binding => (reference (thisEthernet)) applies to eth2;
			Actual_Connection_Binding => (reference (thisEthernet)) applies to eth3;
			Actual_Connection_Binding => (reference (thisEthernet)) applies to eth4;
			Actual_Connection_Binding => (reference (thisEthernet)) applies to eth5;
			Actual_Connection_Binding => (reference (thisEthernet)) applies to eth6;
			Actual_Connection_Binding => (reference (thisEthernet)) applies to printerConn;
			Actual_Connection_Binding => (reference (thisEthernet)) applies to printerStatusConn;
			Actual_Connection_Binding => (reference (thisEthernet)) applies to touchPanelConn;
			Actual_Connection_Binding => (reference (thisEthernet)) applies to lcdConn;
			Actual_Connection_Binding => (reference (thisEthernet)) applies to coinActivateConn;
			Actual_Connection_Binding => (reference (thisEthernet)) applies to coinConn;
			Actual_Connection_Binding => (reference (thisEthernet)) applies to retChangeConn;
			Actual_Connection_Binding => (reference (thisEthernet)) applies to cardActivateConn;
			Actual_Connection_Binding => (reference (thisEthernet)) applies to cardSucessConn;
			Actual_Connection_Binding => (reference (thisEthernet)) applies to gsmConn;
			
			SEI::BandWidthBudget => 50.0 MBytesps applies to BAC1;
			SEI::BandWidthBudget => 50.0 MBytesps applies to BAC2;
			SEI::BandWidthBudget => 15.0 MBytesps applies to eth0;
			SEI::BandWidthBudget => 1.5 MBytesps applies to eth1;
			SEI::BandWidthBudget => 0.5 MBytesps applies to eth2;
			SEI::BandWidthBudget => 10.0 MBytesps applies to eth3;
			SEI::BandWidthBudget => 0.5 MBytesps applies to eth4;
			SEI::BandWidthBudget => 1.5 MBytesps applies to eth5;
			SEI::BandWidthBudget => 2.5 MBytesps applies to eth6;

	end TicketMachine.impl;
	
	
end TicketMachine;