package TempCtl
public
	with SEI_bootleg;
	with Base_Types;
	with Data_Model;
	data TemperatureType
		properties
			Data_Model::Data_Representation => Integer;
	end TemperatureType;
	
	data implementation TemperatureType.impl
	end TemperatureType.impl;
	
	bus ethernet
		properties
			 -- Transmission_Time => [Fixed => 1ns..2ns; PerByte => 5ns .. 6ns;];
			 -- Period => 1ms;
			SEI_bootleg::GrossWeight => 0.5kg;
			SEI_bootleg::BandWidthCapacity => 2.0 MBytesps;
			SEI_bootleg::BandWidthBudget => 200.0 KBytesps;
	end ethernet;
	
	device TempSensor
		features
			Temp: out data port TemperatureType;
			BA1: requires bus access ethernet;
		properties
			SEI_bootleg::GrossWeight => 0.09kg;
			Dispatch_Protocol => Periodic;
			Period => 20ms;
	end TempSensor;
	
	device implementation TempSensor.impl
	end TempSensor.impl;
	
	device Radiator
		features OnOffRad: in data port Base_Types::Boolean;
		properties
			SEI_bootleg::GrossWeight => 1.0kg;
	end Radiator;
	
	device implementation Radiator.impl
	end Radiator.impl;
	
	thread thrSensor
		features
			TempIn: in data port TemperatureType;
			TempOut: out data port TemperatureType;
	end thrSensor;
	
	thread implementation thrSensor.impl
	end thrSensor.impl;
	
	thread thrCtl
		features
			TempIn: in data port TemperatureType;
			onRadOut: out data port Base_Types::Boolean;
	end thrCtl;
	
	thread implementation thrCtl.impl
	end thrCtl.impl;
	
	thread thrRad
		features
			onRadIn: in data port Base_Types::Boolean;
			onRadOut: out data port Base_Types::Boolean;
	end thrRad;
	
	thread implementation thrRad.impl
	end thrRad.impl;
	
	
	memory RAM
		features
			BA1: requires bus access ethernet;
		properties
			SEI_bootleg::GrossWeight => 0.3kg;
	end RAM;
	
	memory implementation RAM.Standard
	end RAM.Standard;
	
	
	bus implementation ethernet.Impl
	end ethernet.Impl;
	
	
	process proc
		features
			TempIn: in data port TemperatureType;
			onRadOut: out data port Base_Types::Boolean;
		properties
			SEI_bootleg::MIPSBudget => 1.0 mips;
	end proc;
	
	process implementation proc.impl
		subcomponents
			tTemp: thread thrSensor.impl;
			tCtl: thread thrCtl.impl; 
			tRad: thread thrRad.impl;
		connections
			tempDevConn: port TempIn -> tTemp.TempIn;
			tempConn: port tTemp.TempOut -> tCtl.TempIn;
			radConn: port tCtl.onRadOut -> tRad.onRadIn;
			radDevConn: port tRad.onRadOut -> onRadOut;
	end proc.impl;
	
	
	processor corei5
		features
			eth: requires bus access ethernet;
		properties
			SEI_bootleg::GrossWeight => 0.1kg;
			Scheduling_Protocol => (Round_Robin_Protocol);
			Clock_Period => 1 ms;
			Timing_Properties::Processor_Capacity => 1.0 MIPS;
	end corei5;
	
	
	system SysTemp
	end SysTemp;
	
	system implementation SysTemp.impl
		subcomponents
			Ctl_cpu: processor corei5;
			thisCtlProc: process proc.impl;
			thisSensor: device TempSensor.impl;
			thisRadiator: device Radiator.impl;
			Stand_Memory: memory RAM.Standard;
			thisEthernet: bus ethernet.impl;
		connections
			tempConn: port thisSensor.Temp -> thisCtlProc.TempIn;
			radConn: port thisCtlProc.onRadOut -> thisRadiator.OnOffRad;
			eth1: bus access thisSensor.BA1 -> thisEthernet;
			eth2: bus access Stand_Memory.BA1 <-> thisEthernet;
			ethProc: bus access Ctl_cpu.eth <-> thisEthernet;
		properties
			SEI_bootleg::GrossWeight => 1.990kg;
			Actual_Processor_Binding => (reference(Ctl_cpu)) applies to thisCtlProc;
			Actual_Connection_Binding => (reference(thisEthernet)) applies to tempConn;
			Actual_Connection_Binding => (reference(thisEthernet)) applies to radConn;
			
	end SysTemp.impl;
	
	
end TempCtl;