
package TempCtl
public
	with Base_Types;
	with Data_Model;
	data TemperatureType
		properties
			Data_Model::Data_Representation => Integer;
	end TemperatureType;
	
	data implementation TemperatureType.impl
	end TemperatureType.impl;
	
	device TempSensor
		features
			Temp: out data port TemperatureType;
			BA1: requires bus access ethernet;
		properties
			Dispatch_Protocol => Periodic;
			Period => 20ms;
	end TempSensor;
	
	device implementation TempSensor.impl
	end TempSensor.impl;
	
	device Radiator
		features OnOffRad: in data port Base_Types::Boolean;
	end Radiator;
	
	device implementation Radiator.impl
	end Radiator.impl;
	
	thread thrSensor
		features
			TempIn: in data port TemperatureType;
			TempOut: out data port TemperatureType;
	end thrSensor;
	
	thread implementation thrSensor.impl
	end thrSensor.impl;
	
	thread thrCtl
		features
			TempIn: in data port TemperatureType;
			onRadOut: out data port Base_Types::Boolean;
	end thrCtl;
	
	thread implementation thrCtl.impl
	end thrCtl.impl;
	
	thread thrRad
		features
			onRadIn: in data port Base_Types::Boolean;
			onRadOut: out data port Base_Types::Boolean;
	end thrRad;
	
	thread implementation thrRad.impl
	end thrRad.impl;
	
	
	memory RAM
		features
			BA1: requires bus access ethernet;
	end RAM;
	
	memory implementation RAM.Standard
	end RAM.Standard;
	
	bus ethernet
	end ethernet;
	
	bus implementation ethernet.Impl
	end ethernet.Impl;
	
	
	process proc
		features
			TempIn: in data port TemperatureType;
			onRadOut: out data port Base_Types::Boolean;
	end proc;
	
	process implementation proc.impl
		subcomponents
			tTemp: thread thrSensor.impl;
			tCtl: thread thrCtl.impl;
			tRad: thread thrRad.impl;
		connections
			tempDevConn: port TempIn -> tTemp.TempIn;
			tempConn: port tTemp.TempOut -> tCtl.TempIn;
			radConn: port tCtl.onRadOut -> tRad.onRadIn;
			radDevConn: port tRad.onRadOut -> onRadOut;
	end proc.impl;
	
	
	processor corei5
		features
			eth: requires bus access ethernet;
		properties
			Scheduling_Protocol => (Round_Robin_Protocol);
			Clock_Period => 1 ms;
			Timing_Properties::Processor_Capacity => 1.0 MIPS;
	end corei5;
	
	
	system SysTemp
	end SysTemp;
	
	system implementation SysTemp.impl
		subcomponents
			Ctl_cpu: processor corei5;
			thisCtlProc: process proc.impl;
			thisSensor: device TempSensor.impl;
			thisRadiator: device Radiator.impl;
			Stand_Memory: memory RAM.Standard;
		connections
			tempConn: port thisSensor.Temp -> thisCtlProc.TempIn;
			radConn: port thisCtlProc.onRadOut -> thisRadiator.OnOffRad;
		properties
			Actual_Processor_Binding => (reference(Ctl_cpu)) applies to thisCtlProc;
	end SysTemp.impl;
	
	
end TempCtl;